plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: "org.springframework.boot"
    apply plugin: 'io.spring.dependency-management'

    group = 'com.zbutwialypiernik'
    version = '0.0.1'

    sourceCompatibility = '11'

    ext {
        set('springCloudVersion', "Hoxton.SR3")
        set('mockitoVersion', "3.3.3")
        set('junitVersion', "5.3.1")
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // RSA
        implementation 'org.bouncycastle:bcpkix-jdk15on:1.58'

        // Logging
        implementation 'org.apache.logging.log4j:log4j-api:2.13.1'
        implementation 'org.apache.logging.log4j:log4j-core:2.13.1'

        runtimeOnly 'org.springframework.boot:spring-boot-devtools:2.2.6.RELEASE'

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"

    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}

project(':core') {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        // Validator
        implementation 'org.passay:passay:1.5.0'
        implementation "org.hibernate:hibernate-validator:6.1.2.Final"

        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        //Tests
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':core-resource') {
    repositories {
        maven {
            url "http://dl.bintray.com/ijabz/maven"
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation project(":core")
        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // Image resizing
        implementation 'net.coobird:thumbnailator:0.4.11'

        // Managing static content
        implementation "com.github.paulcwarren:spring-content-fs-boot-starter:1.0.0.M9"

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'javax.persistence:javax.persistence-api:2.2'

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        implementation 'net.jthink:jaudiotagger:2.2.5'

        //Tests
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':core-authentication') {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation project(":core")

        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'javax.persistence:javax.persistence-api:2.2'

        implementation 'org.springframework.boot:spring-boot-starter-security'

        //Tests
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':service-gateway') {
    dependencies {
        implementation project(':core-authentication')

        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        implementation 'org.springframework.boot:spring-boot-starter-security'

        runtimeOnly 'mysql:mysql-connector-java'

        testRuntimeOnly 'com.h2database:h2'
    }
}

project(':service-discovery') {
    dependencies {
        implementation project(':core-authentication')

        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        runtimeOnly 'mysql:mysql-connector-java'

        testRuntimeOnly 'com.h2database:h2'
    }
}

project(':service-authentication') {
    dependencies {
        implementation project(':core')
        implementation project(':core-authentication')

        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // JWT
        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // Spring
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Validation
        implementation "org.hibernate:hibernate-validator:6.1.2.Final"

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        runtimeOnly 'mysql:mysql-connector-java'

        testRuntimeOnly 'com.h2database:h2'
    }
}

project(':service-resource') {
    repositories {
        maven {
            url "http://dl.bintray.com/ijabz/maven"
        }
    }

    ext {
        restAssuredVersion = '4.0.0'
    }

    dependencies {
        implementation project(':core')
        implementation project(':core-resource')

        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // Managing static content
        implementation "com.github.paulcwarren:spring-content-fs-boot-starter:1.0.0.M9"

        // Spring
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Validation
        implementation "org.hibernate:hibernate-validator:6.1.2.Final"

        runtimeOnly 'mysql:mysql-connector-java'

        testImplementation "io.rest-assured:json-path:$restAssuredVersion"
        testImplementation "io.rest-assured:xml-path:$restAssuredVersion"
        testImplementation "io.rest-assured:spring-mock-mvc:$restAssuredVersion"
        testImplementation "io.rest-assured:rest-assured-all:$restAssuredVersion"
        testImplementation 'org.hamcrest:hamcrest-all:1.3'

        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        // Database
        testRuntimeOnly 'com.h2database:h2'
    }
}