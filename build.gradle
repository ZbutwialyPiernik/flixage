plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: "org.springframework.boot"
    apply plugin: 'io.spring.dependency-management'

    group = 'com.zbutwialypiernik'
    version = '0.0.1'

    sourceCompatibility = '11'

    ext {
        set('springCloudVersion', "Hoxton.SR3")
        set('mockitoVersion', "3.3.3")
        set('junitVersion', "5.3.1")
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // RSA
        implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

        // Logging
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.1'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}

project(':core') {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // Validation
        implementation "org.hibernate:hibernate-validator:6.1.2.Final"
        implementation 'org.passay:passay:1.5.0'

        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // Managing static content
        implementation "com.github.paulcwarren:spring-content-fs-boot-starter:1.0.0.M9"
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'


        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        implementation 'javax.persistence:javax.persistence-api:2.2'

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        //Tests
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':gateway') {
    apply plugin: 'war'

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

        // Database
        runtimeOnly 'com.h2database:h2'
    }
}

project(':discovery') {

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        runtimeOnly 'com.h2database:h2'
    }
}

project(':authentication') {
    dependencies {
        implementation project(':core')

        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // JWT
        implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
        runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
                'io.jsonwebtoken:jjwt-jackson:0.11.1'

        // Spring
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        // Validation
        implementation "org.hibernate:hibernate-validator:6.1.2.Final"

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'
    }
}

project(':resource-server') {

    dependencies {
        implementation project(':core')

        // Object Mapping
        implementation 'ma.glasnost.orika:orika-core:1.5.4'

        // Managing static content
        implementation "com.github.paulcwarren:spring-content-fs-boot-starter:1.0.0.M9"

        // Database
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        // Spring
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        implementation "org.hibernate:hibernate-validator:6.1.2.Final"

        //providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

        // Database
        runtimeOnly 'com.h2database:h2'
    }
}

project(':admin-panel') {
    dependencies {
        implementation project(':core')
    }
}